Let's make a game!
	name: Dyson Sphere Simulator
	by: TypeNull
	desc: A game about taking over a galaxy with Dyson spheres.
	created: 5/8/2025
	version:0.7

Settings
	building cost increase: 100%

Achievements
*4power
	name:Spark
	req:4 power:ps
	desc:A small spark in this vast world.
*1mPanels
	name:One MILLION panels
	desc:One million panels? In this economy? It's more likely than you think.
	req:1000000 solarPanel
Resources
*iron
	name:Iron
	desc:Iron, the basis of technology. Right?
*power
	name:Power
	desc:The very lifeblood of electronics.
*research
	name:Research
	desc:Little MacGuffins that help you learn stuff.
	req:(1mPanels)
*copper
	name:Copper
	desc:Copper. Might be useful, somehow.
	req:copperMining
*silicon
	name:Silicon
	desc:Silicon, the funny stuff computer boards are made of. How exactly did you get this far without it?
	req:siliconMining
*gigawatt
	name:Gigawatts
	desc:Each one is one BILLION watts! Isn't that wild?
	req:energyCondensing
Buttons
*mine
	name:Mine
	desc:Pulling up your sleeves, and getting iron yourself. Gives 1 iron per click.
	on click:yield 1 iron
/*
*siliconMine
	name:Silicon Mine
	desc:temp
	on click:yield 1 silicon
*/
Buildings
// Basic Buildings
*solarPanel
	name:Solar Panel
	desc:A small solar panel, ready to make power orbiting a star. Makes 1 watt of power per second.
	on tick:yield 1 power
	cost:3 iron
*miningBot
	name:Mining Bot
	desc:A drone, which can mine for you. Takes 2 watts of power a second to run, though!
	cost:6 iron
	on tick:if (power >= 2) yield 1 iron
	on tick:lose 2 power
	req:(4power)
*panelLine
	name:Solar Panel Assembler
	desc:An assembly line for solar panels. Costs 5 watts of power a second, alongside the building cost of a solar panel.
	cost:15 iron
	on tick:if (power >= 5 and iron >= 3) yield 1 solarPanel
	on tick:lose 5 power
	on tick:lose 3 iron
	req:(4power)
*botLine
	name:Mining Bot Assembler
	desc:<q>"Mark III mining bots. Designed by me, built by me, and you'd best hope not on your land."</q> The Architecht
	req:botLines
	cost:100 iron, 50 copper
	on tick:if (power >= 200 and iron >= 6) yield 1 miningBot
//Copper
*copperBot
	name:Copper Bot
	desc:We can re-use the bots we already have for this! They're a little banged up by now, though...
	req:copperMining
	cost:15 iron
	on tick:if (power >=5) yield 1 copper
	on tick:lose 5 power
*botConverter
	name:Bot Converter
	desc:A machine for automatically converting iron miners into copper miners.
	on tick:if (power >= 20 and iron >= 15 and miningBot >= 1) yield 1 copperBot
	on tick:lose 20 power
	on tick:lose 15 iron
	on tick:lose 1 miningBot
	req:(botLines)
	cost:300 iron
//Silicon
*siliconMiner
	name:Silicon Miner
	desc:The bots won't cut it with this one. Plus, you need some way to refine it.
	cost:200 iron, 150 copper
	req:siliconMining
	on tick:
		if (power >= 500) yield 1 silicon
		lose 500 power
	end
*minerMaker
	name:Miner Assembly Line
	desc:<q>"Pootis miner here!"</q> Medium Munitions Man
	cost:1000 iron, 600 copper, 15 silicon
	req:siliconMining
	on tick:
		if (power >= 5000 and iron >= 200 and copper >= 150) yield 1 siliconMiner
		lose 5000 power
		lose 200 iron
		lose 150 copper
	end
//Automation
*panelLineLine
	name:Solar Panel Assembler Assembler
	desc:Woah, an assembly line for assembly lines? Kinda trippy.
	on tick:
		if (gigawatt >= 15 and iron >= 3) yield 1 panelLine
		lose 15 gigawatt
		lose 3 iron
	end
	cost:9001 iron, 2500 copper, 300 silicon
	req:automation

*botLineLine
	name:Mining Bot Assembler Assembler
	desc:<q>"Holy crap, Louis! Automation beyond mortal comprehension!"</q> James Dragon
	on tick:
		if (gigawatt >= 5 and iron >= 100 and copper >= 50) yield 1 botLine
		lose 5 gigawatt
		lose 100 iron
		lose 50 copper
	end
	cost:300 iron, 150 copper, 200 silicon
	req:automation

*converterMaker
	name:Bot Converter Assembler
	desc:An assembly line for bot converters. Hey, isn't everything a converter when you think about it? just taking stuff and turning it into other stuff.
	on tick:
		if (gigawatt >= 20 and iron >= 300) yield 1 botConverter
		lose 20 gigawatt
		lose 300 iron
	end
	cost:6000 iron, 2000 copper, 500 silicon
	req:automation
*clumperMaker
	name:Panel Clumper Assembler
	desc:These can make those handy little drones that clump panels together.
	on tick:
		if (gigawatt >= 5 and iron >= 800) yield 1 panelClumper
		lose 5 gigawatt
		lose 800 iron
	end
	cost:2500 iron, 3000 copper, 500 silicon
//Gigawatts
*gigawattMaker
	name:Gigawatt Capacitor
	desc:<q>"UNLIMITED POWAAAAAAH!"</q> The Senate
	on tick:
		if (power >= 1000000000) yield 1 gigawatt
		lose 1000000000 power
	end
	cost:900 iron, 900 copper, 900 silicon
	req:energyCondensing
//Research
*researcher
	name:Researcher
	desc:Power goes in, research comes out. How? Dunno, probably the turbo-encabulator. Costs 100 watts a second.
	on tick:
		if (power >= 100) yield 1 research
		lose 100 power
	end
	req:(1mPanels)
	cost:500 iron
	tag:researching
*supercomputer
	name:Supercomputer
	desc:After having a long think, you figure out the plans for a supercomputer! It can research a hundred times faster than your previous method! [?(have energyCondensing)|But, it seems you can power it now!|However, it seems to want an odd energy source, that you can't provide right now. Further research is needed.]
	on tick:if (gigawatt >= 10) yield 100 research
	cost:1500 iron, 900 copper, 100 silicon
	req:moreBrainpower
	tag:researching
//Sphere Forming
*panelClump
	name:Panel Clump
	desc:An efficient little clump of solar panels. These cannot be bought.
	on tick:yield 500 power
	no buy
	req:(panelClumping)
*panelClumper
	name:Panel Clumper
	desc:A handy little drone, that can fuse together solar panels. Aint that nice? Costs 200 watts a second.
	on tick:if (power >= 200 and solarPanel >= 250) yield 1 panelClump
	on tick:lose 200 power
	on tick:lose 250 solarPanel
	req:(panelClumping)
	cost:800 iron

Upgrades
*panelClumping
	name:Panel Clumping
	desc:You have a lot of solar panels, don't you? Well, this will unlock panel clumpers, a type of drone which can do just that! And, it makes them twice ass efficient in the process!
	req:100 research
	cost:250 research
	on earn:yield 1 panelClump
*copperMining
	name:Copper Mining
	desc:Yeah, we found this "copper" stuff. Might be useful.
	req:500 research
	cost:1000 research
*botLines
	name:Bot Assembly
	desc:Finally, automated assembly for mining bots!
	req:1000 research
	cost:1500 research
*siliconMining
	name:Silicon Mining
	desc:The brainpower to finally go to a beach and melt down some sand.
	req:2000 research
	cost:2500 research
	on earn:yield 1 silicon
*moreBrainpower
	name:More Brainpower
	desc:Time to have a long and hard think. Where can I go from here? Is there even any point? Can I make my research better? The answer, as usual, is <b>yes.</b>
	req:3000 research
	cost:5000 research
*energyCondensing
	name:Energy Condensing
	desc:This might do the trick! It can take your measly litle watts and turn them into GIGAwatts, a billion times more powerful!
	req:moreBrainpower, 6000 research
	cost:8000 research
	on earn:yield 1 gigawatt
*automation
	name:Automation
	desc:The possibility of full automation, all right here. Can you handle the sheer efficiency?
	req:9000 research
	cost:11000 research

	
